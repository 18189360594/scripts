#!/usr/bin/perl
# -*- mode: Perl; tab-width: 4; -*-

use File::Find;
use POSIX qw(uname);

my $basedir = "/sw";
my $PACKAGE_PATH = $ENV{PACKAGE_PATH};

# For what OS X major-version are we configured?
my $target_OSX = '10.4';

# For what CPU arch are are built?
my $target_CPU = 'Power Macintosh';

# set to index (16-31) of msg in *.lproj/InstallationCheck.strings if error
my $errmsg = 0;

DO_CHECKS:
{
	print "running InstallationCheck...\n";

	# we're only for virgin systems, not upgrading/overwriting existing fink
	if ((-e "$basedir") || (-l "$basedir")) {
		print "Fink Installer: ROOT /sw exists!\n";
		$errmsg = 19;
		last;
	}

	# x.y.z form (10.4.6, for example)
	my $OSXVersion = qx{defaults read /System/Library/CoreServices/SystemVersion ProductUserVisibleVersion};
	$OSXVersion = '' if !defined $OSXVersion;
	chomp $OSXVersion;

	# make sure OS X version is the distro for which we're configured
	if ($OSXVersion !~ /^\Q$target_OSX./) {
		print "Fink Installer: This Installer is for Mac OS X $target_OSX, not $OSXVersion. Bailing out.\n";
		$errmsg = 20;
		last;
	}

	# human-readable string ("Power Macintosh" or "i386")
	# not a config.guess platform value
	my $CPUArch = (uname())[4];

	# make sure CPU arch is the one for which we're built
	if ($CPUArch ne $target_CPU) {
		print "Fink Installer: This Installer is for $target_CPU machines, not $CPUArch. Bailing out.\n";
		$errmsg = 31;
		last;
	}
}

# see http://developer.apple.com/documentation/DeveloperTools/Conceptual/SoftwareDistribution/Concepts/sd_install_check_ref.html
my $result = 0;  # all is okay
if ($errmsg) {
	if ($errmsg < 16 || $errmsg > 31) {
		$errmsg = 4;  # generic builtin default msg if out-of-bounds
	}
	$result = ( 1 << 6 ) | ( 1 << 5 ) | $errmsg;  # fatal error bit-vector
}
exit $result;
